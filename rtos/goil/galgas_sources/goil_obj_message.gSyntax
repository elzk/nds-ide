#
# Trampoline OS
#
# Trampoline is copyright (c) IRCCyN 2005+
# Trampoline est prot�g� par la loi sur la propri�t� intellectuelle
#
# This software is distributed under the Lesser GNU Public Licence
#
# GOIL message object parser
#
# $Date$
# $Rev$
# $Author$
# $URL$
#

syntax goil_obj_message :
#--- Lexique Reference, used when grammar source file extension is .gGrammar
with "goil_lexique.gLexique";

import lexique goil_lexique in "goil_lexique.gLexique" ;
import option goil_options in "goil_options.gOption" ;
import semantics goil_semantics in "goil_semantics.gSemantics" ;

import "goil_options.gOption" ;
import "goil_semantics.gSemantics" ;

nonterminal <mess>
  ?!@message_map messages  
label parse
;

nonterminal <message_attributes>
  ?!@action_obj    notif
  ?!@action_obj    notif_error
  ?!@lstringlist   acc_apps
  ?!@ident_map     others
  ?!@mess_prop_obj mess_prop  
label parse
;

nonterminal <message_property>
  ?!@mess_prop_obj mess_prop  
label parse
;

nonterminal <notification_property>
  !@action_obj notif  
label parse
;

nonterminal <ssi_attributes>
  ?!@mess_prop_obj mess_prop  
label parse
;

nonterminal <sse_attributes>
  ?!@mess_prop_obj mess_prop  
label parse
;

nonterminal <sde_attributes>
  ?!@mess_prop_obj mess_prop  
label parse
;

nonterminal <sze_attributes>
  ?!@mess_prop_obj mess_prop  
label parse
;

nonterminal <rzi_attributes>
  ?!@mess_prop_obj mess_prop  
label parse
;

nonterminal <rze_attributes>
  ?!@mess_prop_obj mess_prop  
label parse
;

nonterminal <rui_attributes>
  ?!@mess_prop_obj mess_prop  
label parse
;

nonterminal <rqi_attributes>
  ?!@mess_prop_obj mess_prop  
label parse
;

nonterminal <rue_attributes>
  ?!@mess_prop_obj mess_prop  
label parse
;

nonterminal <rqe_attributes>
  ?!@mess_prop_obj mess_prop  
label parse
;

nonterminal <rde_attributes>
  ?!@mess_prop_obj mess_prop  
label parse
;

nonterminal <rzs_attributes>
  ?!@mess_prop_obj mess_prop  
label parse
;

nonterminal <cdatatype>
  !@lstring cdatatype  
label parse
;

nonterminal <transferproperty>
  !@lstring transferproperty  
label parse
;

nonterminal <networkmessage>
  !@lstring net_message  
label parse
;

nonterminal <filter>
  !@filter_prop_obj filter  
label parse
;

nonterminal <mask_x_attribute>
  !@luint64 mask
  !@luint64 x  
label parse
;

nonterminal <mask_attribute>
  !@luint64 mask  
label parse
;

nonterminal <min_max_attribute>
  !@luint64 min
  !@luint64 max  
label parse
;

nonterminal <period_offset_attribute>
  !@luint64 period
  !@luint64 offset  
label parse
;

nonterminal <networkordercallout>
  !@lstring net_order_callout  
label parse
;

nonterminal <cpuordercallout>
  !@lstring cpu_order_callout  
label parse
;

nonterminal <initialvalue>
  !@basic_type initial_value  
label parse
;

nonterminal <sendingmessage>
  !@lstring sending_message  
label parse
;

nonterminal <queuesize>
  !@luint64 queue_size  
label parse
;

nonterminal <link>
  !@link_obj link  
label parse
;

#
# external nonterminals
#
nonterminal <description>
  !@lstring desc  
label parse
;
nonterminal <free_field>
  ?!@ident_map ids  
label parse
;

rule <mess>
  ?!@message_map messages
:
  @lstring message_name ;
  @message_obj   mess ;
  @lstring       desc ;
  @mess_prop_obj mess_prop ;
  @action_obj    notif ;
  @action_obj    notif_error ;
  @lstringlist   acc_apps ;
  @ident_map     others ;

  $MESSAGE$ ; $idf$ ?message_name ; ${$ ;
  if [messages hasKey ![message_name string]] then
    [!?messages del !message_name ?mess] ;
    mess_prop := [mess messageproperty] ;
    notif := [mess notification] ;
    notif_error := [mess notificationerror] ;
    acc_apps := [mess acc_apps] ;
    others := [mess other_fields] ;
    desc := [mess desc] ;
  else
    mess_prop := [@void_mess_prop new !here] ;
    notif := [@void_action new !here] ;
    notif_error := [@void_action new !here] ;
    acc_apps := [@lstringlist emptyList] ;
    others := [@ident_map emptyMap] ;
    desc := [@lstring new !"" !here] ;
    mess := [@message_obj new !desc !notif !notif_error !acc_apps !others !mess_prop] ;
  end if ;

  <message_attributes> !?notif !?notif_error !?acc_apps !?others !?mess_prop ;
  $}$ ;
  <description> ?desc ;
  $;$ ;
  mess := [@message_obj new !desc !notif !notif_error !acc_apps !others !mess_prop] ;
  [!?messages put !message_name !mess] ;
end rule ;

rule <message_attributes>
  ?!@action_obj    notif
  ?!@action_obj    notif_error
  ?!@lstringlist   acc_apps
  ?!@ident_map     others
  ?!@mess_prop_obj mess_prop
:
  repeat
  while
    cast mess_prop :
    when == @void_mess_prop do
    else 
      error here : "MESSAGEPROPERTY attribute already defined" ;
      error [mess_prop location] : "previously defined here" ;
    end cast ;
    $MESSAGEPROPERTY$ ;   $=$ ; <message_property> !?mess_prop ; <description> ?* ; $;$ ;
  while
    cast notif :
    when == @void_action do
    else 
      error here : "NOTIFICATION attribute already defined" ;
      error [notif location] : "previously defined here" ;
    end cast ;
    $NOTIFICATION$ ;      $=$ ; <notification_property> ?notif ; <description> ?* ; $;$ ;
  while
    cast notif_error :
    when == @void_action do
    else 
      error here : "NOTIFICATIONERROR attribute already defined" ;
      error [notif_error location] : "was previously defined here" ;
    end cast ;
    $NOTIFICATIONERROR$ ; $=$ ; <notification_property> ?notif_error ; <description> ?* ; $;$ ;
  while
    @lstring app ;
    $ACCESSING_APPLICATION$ ; $=$ ; $idf$ ?app ; <description> ?* ; $;$ ;
    add_lstring_unique !?acc_apps !app !"ACCESSING_APPLICATION" ;
  while
    <free_field> !?others ;
  end repeat ;
end rule ;

rule <message_property>
  ?!@mess_prop_obj mess_prop
:
  select $SEND_STATIC_INTERNAL$ ;      <ssi_attributes> !?mess_prop ;
  or     $SEND_STATIC_EXTERNAL$ ;      <sse_attributes> !?mess_prop ;
  or     $SEND_DYNAMIC_EXTERNAL$ ;     <sde_attributes> !?mess_prop ;
  or     $SEND_ZERO_INTERNAL$ ;        mess_prop := [@szi_mess_prop new !here] ;
  or     $SEND_ZERO_EXTERNAL$ ;        <sze_attributes> !?mess_prop ;
  or     $RECEIVE_ZERO_INTERNAL$ ;     <rzi_attributes> !?mess_prop ;
  or     $RECEIVE_ZERO_EXTERNAL$ ;     <rze_attributes> !?mess_prop ;
  or     $RECEIVE_UNQUEUED_INTERNAL$ ; <rui_attributes> !?mess_prop ;
  or     $RECEIVE_QUEUED_INTERNAL$ ;   <rqi_attributes> !?mess_prop ;
  or     $RECEIVE_UNQUEUED_EXTERNAL$ ; <rue_attributes> !?mess_prop ;
  or     $RECEIVE_QUEUED_EXTERNAL$ ;   <rqe_attributes> !?mess_prop ;
  or     $RECEIVE_DYNAMIC_EXTERNAL$ ;  <rde_attributes> !?mess_prop ;
  or     $RECEIVE_ZERO_SENDERS$ ;      <rzs_attributes> !?mess_prop ;
  end select ;
end rule;

rule <notification_property>
  !@action_obj notif
:
  select
    $NONE$ ;
    notif := [@void_action new !here] ;
  or
    $SETEVENT$ ; ${$ ;
    @bool has_task := false ;
    @lstring task_name := [@lstring new !"" !here] ;
    @bool has_event := false ;
    @lstring event_name := [@lstring new !"" !here] ;
    repeat
    while
      if has_task then
        error here : "TASK already defined for SETEVENT notification" ;
      end if ;
      $TASK$ ;
      $=$ ;
      $idf$ ?task_name ;
      <description> ?* ; $;$ ;
      has_task := true ;
    while
        if has_event then
            error here : "EVENT already defined for SETEVENT notification" ;
        end if ;
        $EVENT$ ;
        $=$ ;
        $idf$ ?event_name ;
        <description> ?* ; $;$ ;
        has_event := true ;
    end repeat ;
    $}$ ;
    if not has_task then
        error here : "Task not defined for SETEVENT notification" ;
    end if ;
    if not has_event then
        error here : "Event not defined for SETEVENT notification" ;
    end if ;
    notif := [@setevent_action new !here !task_name !event_name] ;
  or
      @lstring task_name ;
      $ACTIVATETASK$ ; ${$ ;
      $TASK$ ; $=$ ; $idf$ ?task_name ;
      <description> ?* ; $;$ ;
      $}$ ;        
      notif := [@activatetask_action new !here !task_name] ;
  or
      @lstring function_name ;
      $COMCALLBACK$ ; ${$ ;
      $COMCALLBACKNAME$ ; $=$ ; $string$ ?function_name ;
      <description> ?* ; $;$ ;
      $}$ ; 
      notif := [@callback_action new !here !function_name] ;
  or
      @lstring flag_name ;
      $FLAG$ ; ${$ ;
      $FLAGNAME$ ; $=$ ; $string$ ?flag_name ;
      <description> ?* ; $;$ ;
      $}$ ;        
      notif := [@flag_action new !here !flag_name] ;
  or
      @bool has_function := false ;
      @lstring function_name := [@lstring new !"" !here] ;
      @bool has_ipdu := false ;
      @luint64 ipdu := [@luint64 new !0L !here] ;
      $INMCALLBACK$ ; ${$ ;
      repeat
      while
          if has_function then
              error here : "Callback routine already defined for NMCALLBACK notification" ;
          end if ;
          $CALLBACKROUTINENAME$ ; $=$ ; $string$ ?function_name ; <description> ?* ; $;$ ;
          has_function := true ;
      while
          if has_ipdu then
              error here : "Callback routine already defined for NMCALLBACK notification" ;
          end if ;
          $MONITOREDIPDU$ ; $=$ ; $uint_number$ ?ipdu ; <description> ?* ; $;$ ;
          has_ipdu := true ;
      end repeat ;
      $}$ ; 
      if not has_function then
          error here : "Callback not defined for NMCALLBACK notification" ;
      end if ;
      if not has_ipdu then
          error here : "IPDU not defined for NMCALLBACK notification" ;
      end if ;
      notif := [@nmcallback_action new !here !function_name !ipdu] ;
  end select ;
end rule;

rule <ssi_attributes>
    ?!@mess_prop_obj mess_prop
:
    @lstring cdatatype ;

    ${$ ;
    <cdatatype> ?cdatatype ;
    $}$ ;
    mess_prop := [@ssi_mess_prop new !here !cdatatype] ;
end rule ;

rule <sse_attributes>
    ?!@mess_prop_obj mess_prop
:
    @bool has_cdatatype := false;
    @lstring cdatatype := [@lstring new !"" !here] ;
    @bool has_transferproperty := false;
    @lstring transferproperty := [@lstring new !"" !here] ;
    @bool has_net_message := false ;
    @lstring net_message := [@lstring new !"" !here] ;
    @bool has_filter := false ;
    @filter_prop_obj filter := [@void_filter new !here] ;
    @bool has_net_order_callout := false ;
    @lstring net_order_callout := [@lstring new !"" !here] ;
    @bool has_cpu_order_callout := false ;
    @lstring cpu_order_callout := [@lstring new !"" !here] ;
    @bool has_initial_value := false ;
    @basic_type initial_value := [@void new !here] ;

    ${$ ;
    repeat
    while
        if has_cdatatype
        then error here : "CDATATYPE subattribute already defined" ;
        end if ;
        <cdatatype> ?cdatatype ;
        has_cdatatype := true ;
    while
        if has_transferproperty
        then error here : "TRANSFERPROPERTY subattribute already defined" ;
        end if ;
        <transferproperty> ?transferproperty ;
        has_transferproperty := true ;
    while
        if has_net_message
        then error here : "NETWORKMESSAGE subattribute already defined" ;
        end if ;
        <networkmessage> ?net_message ;
        has_net_message := true ;
    while
        if has_filter
        then error here : "FILTER subattribute already defined" ;
        end if ;
        <filter> ?filter ;
        has_filter := true ;
    while
        if has_net_order_callout
        then error here : "NETWORKORDERCALLOUT subattribute already defined" ;
        end if ;
        <networkordercallout> ?net_order_callout ;
        has_net_order_callout := true ;
    while
        if has_cpu_order_callout
        then error here : "CPUORDERCALLOUT subattribute already defined" ;
        end if ;
        <cpuordercallout> ?cpu_order_callout ;
        has_cpu_order_callout := true ;
    while
        if has_initial_value
        then error here : "INITIALVALUE subattribute already defined" ;
        end if ;
        <initialvalue> ?initial_value ;
        has_initial_value := true ;
    end repeat ;
    $}$ ;
    if not has_cdatatype then
        error here : "CDATATYPE is missing in the message" ;
    end if ;
    if not has_transferproperty then
        error here : "TRANSFERPROPERTY is missing in the message" ;
    end if ;
    if not has_net_message then
        error here : "NETWORKMESSAGE is missing in the message" ;
    end if ;
    
    mess_prop := [@sse_mess_prop new
        !here 
        !cdatatype
        !transferproperty
        !filter
        !net_order_callout
        !cpu_order_callout
        !initial_value
        !net_message
    ] ;
end rule ;

rule <sde_attributes>
    ?!@mess_prop_obj mess_prop
:
    @bool has_transferproperty := false;
    @lstring transferproperty := [@lstring new !"" !here] ;
    @bool has_net_message := false;
    @lstring net_message := [@lstring new !"" !here] ;
    @bool has_net_order_callout := false ;
    @lstring net_order_callout := [@lstring new !"" !here] ;
    @bool has_cpu_order_callout := false ;
    @lstring cpu_order_callout := [@lstring new !"" !here] ;
    @bool has_initial_value := false ;
    @basic_type initial_value := [@void new !here] ;

    ${$ ;
    repeat
    while
        if has_transferproperty
        then error here : "TRANSFERPROPERTY subattribute already defined" ;
        end if ;
        <transferproperty> ?transferproperty ;
        has_transferproperty := true ;
    while
        if has_net_message
        then error here : "NETWORKMESSAGE subattribute already defined" ;
        end if ;
        <networkmessage> ?net_message ;
        has_net_message := true ;
    while
        if has_net_order_callout
        then error here : "NETWORKORDERCALLOUT subattribute already defined" ;
        end if ;
        <networkordercallout> ?net_order_callout ;
        has_net_order_callout := true ;
    while
        if has_cpu_order_callout
        then error here : "CPUORDERCALLOUT subattribute already defined" ;
        end if ;
        <cpuordercallout> ?cpu_order_callout ;
        has_cpu_order_callout := true ;
    while
        if has_initial_value
        then error here : "INITIALVALUE subattribute already defined" ;
        end if ;
        <initialvalue> ?initial_value ;
        has_initial_value := true ;
    end repeat ;
    $}$ ;
    mess_prop := [@sde_mess_prop new
        !here
        !transferproperty
        !net_order_callout
        !cpu_order_callout
        !initial_value
        !net_message
    ] ;
end rule ;

rule <sze_attributes>
    ?!@mess_prop_obj mess_prop
:
    @bool has_net_message := false;
    @lstring net_message := [@lstring new !"" !here] ;
    @bool has_net_order_callout := false ;
    @lstring net_order_callout := [@lstring new !"" !here] ;
    @bool has_cpu_order_callout := false ;
    @lstring cpu_order_callout := [@lstring new !"" !here] ;

    ${$ ;
    repeat
    while
        if has_net_message
        then error here : "NETWORKMESSAGE subattribute already defined" ;
        end if ;
        <networkmessage> ?net_message ;
        has_net_message := true ;
    while
        if has_net_order_callout
        then error here : "NETWORKORDERCALLOUT subattribute already defined" ;
        end if ;
        <networkordercallout> ?net_order_callout ;
        has_net_order_callout := true ;
    while
        if has_cpu_order_callout
        then error here : "CPUORDERCALLOUT subattribute already defined" ;
        end if ;
        <cpuordercallout> ?cpu_order_callout ;
        has_cpu_order_callout := true ;
    end repeat ;
    $}$ ;
    if not has_net_message
    then error here : "NETWORKMESSAGE subattribute missing" ;
    end if ;
    mess_prop := [@sze_mess_prop new
        !here
        !net_order_callout
        !cpu_order_callout
        !net_message
    ] ;
end rule ;

rule <rzi_attributes>
    ?!@mess_prop_obj mess_prop
:
    @lstring sending_message ;
    ${$ ;
    <sendingmessage> ?sending_message ;
    $}$ ;

    mess_prop := [@rzi_mess_prop new
        !here
        !sending_message
    ] ;
end rule ;

rule <rze_attributes>
    ?!@mess_prop_obj mess_prop
:
    @bool has_net_message := false;
    @lstring net_message := [@lstring new !"" !here] ;
    @bool has_net_order_callout := false ;
    @lstring net_order_callout := [@lstring new !"" !here] ;
    @bool has_cpu_order_callout := false ;
    @lstring cpu_order_callout := [@lstring new !"" !here] ;

    ${$ ;
    repeat
    while
        if has_net_message
        then error here : "NETWORKMESSAGE subattribute already defined" ;
        end if ;
        <networkmessage> ?net_message ;
        has_net_message := true ;
    while
        if has_net_order_callout
        then error here : "NETWORKORDERCALLOUT subattribute already defined" ;
        end if ;
        <networkordercallout> ?net_order_callout ;
        has_net_order_callout := true ;
    while
        if has_cpu_order_callout
        then error here : "CPUORDERCALLOUT subattribute already defined" ;
        end if ;
        <cpuordercallout> ?cpu_order_callout ;
        has_cpu_order_callout := true ;
    end repeat ;
    $}$ ;
    if not has_net_message
    then error here : "NETWORKMESSAGE subattribute missing" ;
    end if ;
    mess_prop := [@rze_mess_prop new
        !here
        !net_order_callout
        !cpu_order_callout
        !net_message
    ] ;
end rule ;

rule <rui_attributes>
    ?!@mess_prop_obj mess_prop
:
    @bool has_sending_message := false ;
    @lstring sending_message := [@lstring new !"" !here] ;
    @bool has_filter := false ;
    @filter_prop_obj filter := [@void_filter new !here] ;
    @bool has_initial_value := false ;
    @basic_type initial_value := [@void new !here] ;

    ${$ ;
    repeat
    while
        if has_sending_message
        then error here : "SENDINGMESSAGE subattribute already defined" ;
        end if ;
        <sendingmessage> ?sending_message ;
        has_sending_message := true ;
    while
        if has_filter
        then error here : "FILTER subattribute already defined" ;
        end if ;
        <filter> ?filter ;
        has_filter := true ;
    while
        if has_initial_value
        then error here : "INITIALVALUE subattribute already defined" ;
        end if ;
        <initialvalue> ?initial_value ;
        has_initial_value := true ;
    end repeat ;
    $}$ ;
    if not has_sending_message
    then error here : "SENDINGMESSAGE subattribute missing" ;
    end if ;
    mess_prop := [@rui_mess_prop new
        !here
        !sending_message
        !filter
        !initial_value
    ] ;
end rule ;

rule <rqi_attributes>
    ?!@mess_prop_obj mess_prop
:
    @bool has_sending_message := false ;
    @lstring sending_message := [@lstring new !"" !here] ;
    @bool has_filter := false ;
    @filter_prop_obj filter := [@void_filter new !here] ;
    @bool has_queue_size := false ;
    @luint64 queue_size := [@luint64 new !0L !here] ;
    ${$ ;
    repeat
    while
        if has_sending_message
        then error here : "SENDINGMESSAGE subattribute already defined" ;
        end if ;
        <sendingmessage> ?sending_message ;
        has_sending_message := true ;
    while
        if has_filter
        then error here : "FILTER subattribute already defined" ;
        end if ;
        <filter> ?filter ;
        has_filter := true ;
    while
        if has_queue_size
        then error here : "QUEUESIZE subattribute already defined" ;
        end if ;
        <queuesize> ?queue_size ;
        has_queue_size := true ;
    while
        warning here: "INITIALVALUE subattribute ignore for queued messages";
        <initialvalue> ?* ;
    end repeat ;
    $}$ ;
    if not has_sending_message
    then error here : "SENDINGMESSAGE subattribute missing" ;
    end if ;
    if not has_queue_size
    then error here : "QUEUESIZE subattribute missing" ;
    end if ;
    mess_prop := [@rqi_mess_prop new
        !here
        !sending_message
        !filter
        !queue_size
    ] ;
end rule ;

rule <rue_attributes>
    ?!@mess_prop_obj mess_prop
:
    @bool has_cdatatype := false;
    @lstring cdatatype := [@lstring new !"" !here] ;
    @bool has_filter := false ;
    @filter_prop_obj filter := [@void_filter new !here] ;
    @bool has_initial_value := false ;
    @basic_type initial_value := [@void new !here] ;
    @link_obj link := [@void_link new !here] ;

    ${$ ;
    repeat
    while
        if has_cdatatype
        then error here : "CDATATYPE subattribute already defined" ;
        end if ;
        <cdatatype> ?cdatatype ;
        has_cdatatype := true ;
    while
        if has_filter
        then error here : "FILTER subattribute already defined" ;
        end if ;
        <filter> ?filter ;
        has_filter := true ;
    while
        cast link :
        when == @void_link do
        else error here : "LINK subattribute already defined" ;
        end cast ;
        <link> ?link ;
    while
        if has_initial_value
        then error here : "INITIALVALUE subattribute already defined" ;
        end if ;
        <initialvalue> ?initial_value ;
        has_initial_value := true ;
    end repeat ;
    $}$ ;
    if not has_cdatatype then
        error here : "CDATATYPE is missing in the message" ;
    end if ;
    cast link :
    when == @void_link do
        error here : "LINK subattribute missing" ;
    else
    end cast ;

    mess_prop := [@rue_mess_prop new
        !here
        !cdatatype
        !filter
        !link
        !initial_value
    ] ;
end rule ;

rule <rqe_attributes>
    ?!@mess_prop_obj mess_prop
:
    @bool has_cdatatype := false;
    @lstring cdatatype := [@lstring new !"" !here] ;
    @bool has_filter := false ;
    @filter_prop_obj filter := [@void_filter new !here] ;
    @bool has_queue_size := false ;
    @luint64 queue_size := [@luint64 new !0L !here] ;
    @link_obj link := [@void_link new !here] ;

    ${$ ;
    repeat
    while
        if has_cdatatype
        then error here : "CDATATYPE subattribute already defined" ;
        end if ;
        <cdatatype> ?cdatatype ;
        has_cdatatype := true ;
    while
        if has_filter
        then error here : "FILTER subattribute already defined" ;
        end if ;
        <filter> ?filter ;
        has_filter := true ;
    while
        if has_queue_size
        then error here : "QUEUESIZE subattribute already defined" ;
        end if ;
        <queuesize> ?queue_size ;
        has_queue_size := true ;
    while
        cast link :
        when == @void_link do
        else error here : "LINK subattribute already defined" ;
        end cast ;
        <link> ?link ;
    while
        warning here: "INITIALVALUE subattribute ignore for queued messages";
        <initialvalue> ?* ;
    end repeat ;
    $}$ ;

    if not has_cdatatype then
        error here : "CDATATYPE is missing in the message" ;
    end if ;
    if not has_queue_size
    then error here : "QUEUESIZE subattribute missing" ;
    end if ;
    cast link :
    when == @void_link do
        error here : "LINK subattribute missing" ;
    else
    end cast ;

    mess_prop := [@rqe_mess_prop new
        !here
        !cdatatype
        !queue_size
        !filter
        !link
    ] ;

end rule ;

rule <rde_attributes>
    ?!@mess_prop_obj mess_prop
:
    @link_obj link := [@void_link new !here] ;
    @bool has_initial_value := false ;
    @basic_type initial_value := [@void new !here] ;

    ${$ ;
    repeat
    while
        cast link :
        when == @void_link do
        else error here : "LINK subattribute already defined" ;
        end cast ;
        <link> ?link ;
    while
        if has_initial_value
        then error here : "INITIALVALUE subattribute already defined" ;
        end if ;
        <initialvalue> ?initial_value ;
        has_initial_value := true ;
    end repeat ;
    $}$ ;

    cast link :
    when == @void_link do
        error here : "LINK subattribute missing" ;
    else
    end cast ;

    mess_prop := [@rde_mess_prop new
        !here
        !link
        !initial_value
    ] ;

end rule ;

rule <rzs_attributes>
    ?!@mess_prop_obj mess_prop
:
    @bool has_cdatatype := false;
    @lstring cdatatype := [@lstring new !"" !here] ;
    @bool has_initial_value := false ;
    @basic_type initial_value := [@void new !here] ;

    ${$ ;
    repeat
    while
        if has_cdatatype
        then error here : "CDATATYPE subattribute already defined" ;
        end if ;
        <cdatatype> ?cdatatype ;
        has_cdatatype := true ;
    while
        if has_initial_value
        then error here : "INITIALVALUE subattribute already defined" ;
        end if ;
        <initialvalue> ?initial_value ;
        has_initial_value := true ;
    end repeat ;
    $}$ ;

    if not has_cdatatype then
        error here : "CDATATYPE is missing in the message" ;
    end if ;

    mess_prop := [@rzs_mess_prop new
        !here
        !cdatatype
        !initial_value
    ] ;
end rule ;

rule <cdatatype>
    !@lstring cdatatype
:
    $CDATATYPE$ ;
    $=$ ;
    $string$ ?cdatatype ;
    <description> ?* ; $;$ ;
end rule ;

rule <transferproperty>
    !@lstring transferproperty
:

    $TRANSFERPROPERTY$ ;
    $=$ ;
    select
        $TRIGGERED$ ; transferproperty := [@lstring new !"triggered" !here] ;
    or
        $PENDING$ ; transferproperty := [@lstring new !"pending" !here] ;
    or
        $AUTO$ ; transferproperty := [@lstring new !"auto" !here] ;
    end select ;
    <description> ?* ; $;$ ;
end rule ;

rule <networkmessage>
    !@lstring net_message
:
    $NETWORKMESSAGE$ ;
    $=$ ;
    $idf$ ?net_message ;
    <description> ?* ; $;$ ;
end rule ;

rule <filter>
    !@filter_prop_obj filter
:
    $FILTER$ ;
    $=$ ;
    select
        filter := [@always_filter new !here] ;
        $ALWAYS$ ;
    or
        filter := [@never_filter new !here] ;
        $NEVER$ ;
    or
        @luint64 mask ;
        @luint64 x ;
        @location filterLocation := here;
        $MASKEDNEWEQUALSX$ ;  <mask_x_attribute> ?mask ?x ;
        filter := [@maskednewequalsx_filter new !filterLocation !mask !x] ;
    or
        @luint64 mask ;
        @luint64 x ;
        @location filterLocation := here;
        $MASKEDNEWDIFFERSX$ ; <mask_x_attribute> ?mask ?x ;
        filter := [@maskednewdiffersx_filter new !filterLocation !mask !x] ;
    or
        filter := [@newisequal_filter new !here] ;
        $NEWISEQUAL$ ;
    or
        filter := [@newisdifferent_filter new !here] ;
        $NEWISDIFFERENT$ ;
    or
        @luint64 mask ;
        @location filterLocation := here;
        $MASKEDNEWEQUALSMASKEDOLD$ ;  <mask_attribute> ?mask ;
        filter := [@maskednewequalsmaskedold_filter new !filterLocation !mask] ;
    or
        @luint64 mask ;
        @location filterLocation := here;
        $MASKEDNEWDIFFERSMASKEDOLD$ ; <mask_attribute> ?mask ;
        filter := [@maskednewdiffersmaskedold_filter new !filterLocation !mask] ;
    or
        @luint64 min ;
        @luint64 max ;
        @location filterLocation := here;
        $NEWISWITHIN$ ;  <min_max_attribute> ?min ?max ;
        filter := [@newiswithin new !filterLocation !min !max] ;
    or
        @luint64 min ;
        @luint64 max ;
        @location filterLocation := here;
        $NEWISOUTSIDE$ ; <min_max_attribute> ?min ?max ;
        filter := [@newisoutside new !filterLocation !min !max] ;
    or
        filter := [@newisgreater_filter new !here] ;
        $NEWISGREATER$ ;
    or
        filter := [@newislessorequal_filter new !here] ;
        $NEWISLESSOREQUAL$ ;
    or
        filter := [@newisless_filter new !here] ;
        $NEWISLESS$ ;
    or
        filter := [@newisgreaterorequal_filter new !here] ;
        $NEWISGREATEROREQUAL$ ;
    or
        @luint64 period ;
        @luint64 offset ;
        @location filterLocation := here;
        $ONEEVERYN$ ;    <period_offset_attribute> ?period ?offset;
        filter := [@oneeveryn_filter new !filterLocation !period !offset] ;
    end select ;
    <description> ?* ; $;$ ;
end rule ;

rule <mask_x_attribute>
    !@luint64 mask
    !@luint64 x
:
    @bool has_mask := false ;
    @bool has_x := false ;
    mask := [@luint64 new !0L !here] ;
    x := [@luint64 new !0L !here] ;

    ${$ ;
    repeat
    while
        if has_mask then
            error here : "MASK is already defined for the filter" ;
        end if ;
        $MASK$ ; $=$ ; $uint_number$ ?mask ; <description> ?* ; $;$ ;
        has_mask := true ;
    while
        if has_x then
            error here : "X is already defined for the filter" ;
        end if ;
        $X$ ; $=$ ; $uint_number$ ?x ; <description> ?* ; $;$ ;
        has_x := true ;
    end repeat ;
    $}$ ;
    if not has_mask then
        error here : "MASK is missing in the filter" ;
    end if ;
    if not has_x then
        error here : "X is missing in the filter" ;
    end if ;
end rule ;

rule <mask_attribute>
    !@luint64 mask
:
    ${$ ;
    $MASK$ ; $=$ ; $uint_number$ ?mask ; <description> ?* ; $;$ ;
    $}$ ;
end rule ;

rule <min_max_attribute>
    !@luint64 min
    !@luint64 max
:
    @bool has_min := false ;
    @bool has_max := false ;
    min := [@luint64 new !0L !here] ;
    max := [@luint64 new !0L !here] ;

    ${$ ;
    repeat
    while
        if has_min then
            error here : "MIN is already defined for the filter" ;
        end if ;
        $MIN$ ; $=$ ; $uint_number$ ?min ; <description> ?* ; $;$ ;
        has_min := true ;
    while
        if has_max then
            error here : "MAX is already defined for the filter" ;
        end if ;
        $MAX$ ; $=$ ; $uint_number$ ?max ; <description> ?* ; $;$ ;
        has_max := true ;
    end repeat ;
    $}$ ;
    if not has_min then
        error here : "MIN is missing in the filter" ;
    end if ;
    if not has_max then
        error here : "MAX is missing in the filter" ;
    end if ;
end rule ;

rule <period_offset_attribute>
    !@luint64 period
    !@luint64 offset
:
    @bool has_period := false ;
    @bool has_offset := false ;
    period := [@luint64 new !0L !here] ;
    offset := [@luint64 new !0L !here] ;

    ${$ ;
    repeat
    while
        if has_period then
            error here : "PERIOD is already defined for the filter" ;
        end if ;
        $PERIOD$ ; $=$ ; $uint_number$ ?period ; <description> ?* ; $;$ ;
        has_period := true ;
    while
        if has_offset then
            error here : "OFFSET is already defined for the filter" ;
        end if ;
        $OFFSET$ ; $=$ ; $uint_number$ ?offset ; <description> ?* ; $;$ ;
        has_offset := true ;
    end repeat ;
    $}$ ;
    if not has_period then
        error here : "PERIOD is missing in the filter" ;
    end if ;
    if not has_offset then
        error here : "OFFSET is missing in the filter" ;
    end if ;
end rule ;

rule <networkordercallout>
    !@lstring net_order_callout
:
    $NETWORKORDERCALLOUT$ ;
    $=$ ;
    $string$ ?net_order_callout ;
    <description> ?* ; $;$ ;
end rule ;

rule <cpuordercallout>
    !@lstring cpu_order_callout
:
    $CPUORDERCALLOUT$ ;
    $=$ ;
    $string$ ?cpu_order_callout ;
    <description> ?* ; $;$ ;
end rule ;

rule <initialvalue>
    !@basic_type initial_value
:
    $INITIALVALUE$ ;
    $=$ ;
    select
        @luint64 ival ;
        $uint_number$ ?ival ;
        initial_value := [@uint64_class new ![ival location] ![ival uint64]] ;
    or
        @ldouble dval ;
        $float_number$ ?dval ;
        initial_value := [@float_class new ![dval location] ![dval double]] ;
    or
        @lstring sval ;
        $string$ ?sval ;
        initial_value := [@string_class new ![sval location] ![sval string]] ;
    end select ;
    <description> ?* ; $;$ ;
end rule ;

rule <sendingmessage>
    !@lstring sending_message
:
    $SENDINGMESSAGE$ ;
    $=$ ;
    $idf$ ?sending_message ;
    <description> ?* ; $;$ ;
end rule ;

rule <queuesize>
    !@luint64 queue_size
:
    $QUEUESIZE$ ;
    $=$ ;
    $uint_number$ ?queue_size ;
    <description> ?* ; $;$ ;
end rule ;

rule <link>
    !@link_obj link
:
    $LINK$ ;
    $=$ ;
    select
        @lstring rcv_mess ;
        $TRUE$ ; ${$ ;
        $RECEIVEMESSAGE$ ; $=$ ; $idf$ ?rcv_mess ; <description> ?* ; $;$ ;
        $}$ ;
        link := [@true_link new !here!rcv_mess] ;
    or
        @lstring net_mess ;
        $FALSE$ ; ${$ ;
        $NETWORKMESSAGE$ ; $=$ ; $idf$ ?net_mess ; <description> ?* ; $;$ ;
        $}$ ;
        link := [@false_link new !here!net_mess] ;
    end select ;
    <description> ?* ; $;$ ;
end rule ;

end syntax ;
# vim:ft=ggs:ts=4:sw=4
