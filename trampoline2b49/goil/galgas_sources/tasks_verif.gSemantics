##
# @file tasks_verif.ggs
#
# @section File description
#
# routines defined in tasks_verif semantics component
# verify tasks are Ok.
#
# @section Copyright
#
# Goil OIL compiler - Trampoline OS
#
# Trampoline is copyright (c) IRCCyN 2005+
# Trampoline is protected by the french intellectual property law.
#
# This software is distributed under the GNU Public Licence V2
#
# @section File informations
#
# $Date$
# $Rev$
# $Author$
# $URL$
#

semantics tasks_verif:

import semantics goil_types_task in "goil_types_task.gSemantics";

import "goil_types_task.gSemantics";

##
# @fn tasks_well_formed
#
# tasks_well_formed checks each task has all the required
# attributes and whether there is a conflict in attributes.
# Required attributes are:
# - PRIORITY
# - ACTIVATION
# - SCHEDULE
# - AUTOSTART
#
# @param tasks    the map of tasks.
#
routine tasks_well_formed
  ??@task_map tasks
:
  foreach tasks (@lstring task_name @task_obj task) do

    cast [task priority]:
      when == @void do
        error task_name: "PRIORITY attribute not defined for TASK ".[task_name string];
      else
    end cast;

    cast [task activation]:
      when == @void do
        error task_name: "ACTIVATION attribute not defined for TASK ".[task_name string];
      when == @uint32_class ui do
        @lstringlist defined_events:= [task events];
        if ( [defined_events length] > 0 & [ui value] > 1) then
          error task_name: "ACTIVATION attribute for extended task ".[task_name string]." should be set to 1";
          error [ui location]: "ACTIVATION attribute set to ".[[ui value] string]." here";
        end if;
      else
        error here: "Internal error, wrong datatype for ACTIVATION in task ".[task_name string];
    end cast;

    if [[task schedule] string] == "" then
      error task_name: "SCHEDULE attribute not defined for TASK ".[task_name string];
    end if;

    cast [task autostart]:
      when == @task_autostart_void do
        error task_name: "AUTOSTART attribute not defined for TASK ".[task_name string];
      else
    end cast;

  end foreach;
end routine;

end semantics;
# End of file tasks_verif.ggs
